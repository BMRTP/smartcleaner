System smartcleaner

Dispatch explore   : explore(X)
Dispatch suspend   : suspend(X)
Dispatch terminate : terminate(X)
Dispatch continue  : continue(X)

Dispatch cmd      : cmd(X)

Request  step     : step(DURATION)
Request  backstep : backstep(DURATION)
Reply    stepdone : stepdone(X)
Reply    stepfail : stepfail(DURATION)

Request getobstacletype : getobstacletype(X)
Reply   obstacletype    : obstacletype(X)

Request  grab     : grab(X)
Reply    grabbed  : grabbed(X) //true, false

Request throwaway : throwaway(QUANTITY)
Reply   throwed : throwed(QUANTITY)

Dispatch init : init(NDB, WITHMAP)

Context ctxDetector ip [ host= "localhost" port= 8022 ]  
Context ctxRobot ip [ host= "127.0.0.1" port= 8018 ]  
Context ctxPlasticBox ip [ host= "127.0.0.1" port= 8016 ]

ExternalQActor smartrobot context ctxRobot
ExternalQActor obstacleclassifier context ctxRobot
ExternalQActor grabber context ctxRobot
ExternalQActor plasticbox context ctxPlasticBox

QActor detector context ctxDetector { 
	[" 
	   val resource = itunibo.resource.CoapResourceSupport(name, \"coap://localhost:5683\")
	   var NDB = 5
	   var SpaceAvailable = NDB
	   resource.createProperty(\"SpaceAvailable\", SpaceAvailable.toString())

	   var RoomMap = \"\" 
	   resource.createProperty(\"RoomMap\", RoomMap)

	   var currentTask = DetectorTask.IDLE
	   resource.createProperty(\"currentTask\", currentTask.toString())

	   var waitingForSupervisor: Boolean = false
	   resource.createProperty(\"waitingForSupervisor\", waitingForSupervisor.toString())

	   val mapname     = \"roomMbot3\"

	   //VIRTUAL ROBOT
       var StepTime = 330
	   //var StepTime = 700
	   var BackStepTime = 0
       val BackStepTimeCompensation = 20
	   //val BackStepTimeCompensation = 2000
	   var completedPerimeter = false
	   var wasABottle = false

	   var currentPosition = Pair(1,1)
	   var lastPosition = currentPosition
	   val planner = itunibo.planner.plannerUtil
	   var plan = itunibo.planner.RobotPlan.empty()
	   var nextMove = itunibo.planner.PlanMove.NONE

	   var WaitCommandTime = 5L
		"]
	State start initial {  
		println("detector starts")
		run planner.initAI()
	} Transition t
		whenMsg init -> initializing
		whenMsg explore -> exploring
 		whenMsg suspend -> goingHome
 		whenMsg terminate -> terminating
 		
	State initializing {
		onMsg(init : init(NDB, WITHMAP)) {
		  ["NDB = payloadArg(0).toInt()
				SpaceAvailable = NDB
			if (payloadArg(1) == \"true\") {
				val (a, b) = planner.loadRoomMap(mapname)
			    if (a > 0 && b > 0) {
				    completedPerimeter = true
				    RoomMap = planner.getMap()
				    resource.setProperty(\"RoomMap\", RoomMap)
				}
			} "]
		}
	} Goto discoveryHome
	
	State discoveryHome {
		println("STATE: discoveryHome")
		println("waiting for a command...")	
	}
	Transition commandReceived
 			whenMsg explore -> exploring
 			whenMsg suspend -> goingHome
 			whenMsg terminate -> terminating
	
	State exploring { 
		println("STATE: exploring")
	  [ "currentTask = DetectorTask.EXPLORING
		resource.setProperty(\"currentTask\", currentTask.toString()) "]
	} Goto exploringWithPlan if "(completedPerimeter)" else continuePerimeter
	
	State updatePosition {
		println("STATE: updatePosition")
		["currentPosition = planner.getRobotPosition()
		  if (currentPosition.first == 1 && currentPosition.second == 1 && currentTask == DetectorTask.EXPLORING) {
				completedPerimeter = true
		  }
		"]  
	}
	Goto exploringWithPlan if "(completedPerimeter || currentTask != DetectorTask.EXPLORING)" else continuePerimeter
	 
	State continuePerimeter {
		println("STATE: continuePerimeter")
		forward smartrobot -m cmd : cmd(d)
		run planner.rotateRight90()
		delay 1000
	} Goto doStep
	
	State doStep {
		run startTimer()
	} Transition a
		whenTimeVar WaitCommandTime  -> stepping
		whenMsg explore -> exploring
 		whenMsg suspend -> goingHome
 		whenMsg terminate -> terminating
	
	State stepping {
		println("STATE: doStep")
		request smartrobot -m step : step($StepTime)
	}
	Transition t0   whenReply stepdone -> stepDone   
					whenReply stepfail -> stepFailed
	
	State stepDone {  
		println("STATE: stepDone")
 		run planner.updateMapAfterAheadOk()
		[" RoomMap = planner.getMap()
		   resource.setProperty(\"RoomMap\", RoomMap) "]		
		delay 1000
 	}
	Goto updatePosition
	
	State stepFailed {  
		println("STATE: stepFailed")
 		onMsg(stepfail : stepfail(DURATION)) {
 			println("detector founds an obstacle after ${payloadArg(0)}")
 			["BackStepTime = payloadArg(0).toInt() - BackStepTimeCompensation"]
 			request obstacleclassifier -m getobstacletype : getobstacletype(type)
 		}
	}
	Transition t1
		whenReply obstacletype -> checkObstacleType
    
    State checkObstacleType {
    	println("STATE: checkObstacleType")
    	onMsg(obstacletype : obstacletype(X)) {
 			println("detector founds ${payloadArg(0)}")
 			["wasABottle = (payloadArg(0).contains(\"bottle\", ignoreCase = true))"]
 			if "(wasABottle)" {
 				if "(SpaceAvailable > 0)" {
 					request grabber -m grab : grab(it)
 				} else {
 					//already dirty cell
 					["lastPosition = planner.getAHeadPosition() "]
 					request smartrobot -m backstep : backstep($BackStepTime)
 				}
 			} else {
 				run planner.setObstacleOnCurrentDirection()
 				request smartrobot -m backstep : backstep($BackStepTime)
 			}
 			[" RoomMap = planner.getMap()
		   resource.setProperty(\"RoomMap\", RoomMap) "]		
 		}
    }
    Transition t2
    	whenReply grabbed -> bottleGrabbed
    	whenReply stepdone -> backCompensationDone
    	
    State backCompensationDone {
    	println("STATE: backCompensationDone")
    } Goto emptingBox if "(wasABottle)" else doNextStep
    
    State doNextStep {
    	println("STATE: doNextStep")
    	if "(!completedPerimeter)" {
	    	forward smartrobot -m cmd : cmd(a)
	    	run planner.rotateLeft90()
	    	delay 1000
    	}
    } 
    Goto doStep if "(!completedPerimeter)" else resetPlan
    	
    State bottleGrabbed {
    	println("STATE: bottleGrabbed")
    	onMsg(grabbed : grabbed(X)) {
    		if "(payloadArg(0) == \"true\")" {
 				println("detector grabbed a bottle")
 				[" SpaceAvailable -= 1
		   		   resource.setProperty(\"SpaceAvailable\", SpaceAvailable.toString()) "]
 			} else {
 				println("FATAL ERROR: detector can't grab the object")
 			}
 			request smartrobot -m backstep : backstep($BackStepTime)
    	}
    }
    Transition t3
    	 whenReply stepdone -> doStep
  	
  	State resetPlan {
  		println("STATE: resetPlan")
  		["plan = itunibo.planner.RobotPlan.empty()"]
  	} Goto exploringWithPlan
  	
  	State exploringWithPlan {
  		println("STATE: exploringWithPlan")
  		if "(plan.isDone() && !planner.isRoomClean() && currentTask == DetectorTask.EXPLORING)" {
  		  [" val (x,y) = planner.getNextDirtyCell()
		  	 plan = planner.getPlanMoves(x, y)
			 if(plan.isDone()) {
				planner.setObstacle(x, y)
				RoomMap = planner.getMap()
		    	resource.setProperty(\"RoomMap\", RoomMap)
		     } "]
  		} 
  	} Goto terminating if "(planner.isRoomClean() && currentTask == DetectorTask.EXPLORING)" else takeNextMove
  	
  	State takeNextMove {
  		println("STATE: takeNextMove")
  		["nextMove = plan.getNextMove()"]
  	} Goto planDone if "(nextMove == itunibo.planner.PlanMove.NONE)" else checkW
  	
  	State planDone {
  		println("STATE: planDone")
  	} Goto exploringWithPlan if "(currentTask == DetectorTask.EXPLORING)" else destinationReached
  	
  	State checkW {
  		println("STATE: checkW")
  	} Goto doStep if "(nextMove == itunibo.planner.PlanMove.FORWARD)" else checkD
  	
  	State checkD {
  		println("STATE: checkD")
  	} Goto rotateRight if "(nextMove == itunibo.planner.PlanMove.RIGHT)" else checkA
  	
  	State rotateRight {
  		println("STATE: rotateRight")
  		forward smartrobot -m cmd : cmd(d)
		run planner.rotateRight90()
		delay 1000
  	} Goto exploringWithPlan
  	
  	State checkA {
  		println("STATE: checkA")
  	} Goto rotateLeft if "(nextMove == itunibo.planner.PlanMove.LEFT)" else exploringWithPlan
  	
  	State rotateLeft {
  		println("STATE: rotateLeft")
  		forward smartrobot -m cmd : cmd(a)
	    run planner.rotateLeft90()
	    delay 1000
  	} Goto exploringWithPlan
	
	State emptingBox {
		println("STATE: emptingBox")
		["currentTask = DetectorTask.EMPTING_BOX
		  resource.setProperty(\"currentTask\", currentTask.toString()) 
 		  plan = planner.goPlasticBoxMoves() "]
	} Goto exploringWithPlan
	
	State goingHome {
		println("STATE: goingHome")
		println("Detector going home")
		["currentTask = DetectorTask.GOING_HOME
		  resource.setProperty(\"currentTask\", currentTask.toString())
		  plan = planner.goHomeMoves()
		"]
	} Goto exploringWithPlan
	
	State terminating {
		println("STATE: terminating")
		println("Detector terminated the work")
	} Goto goingHome if "(SpaceAvailable == NDB)" else goingPlasticBox
	
	State goingPlasticBox {
		println("STATE: goingPlasticBox")
		println("Detector going to plasticbox")
		["currentTask = DetectorTask.EMPTING_BOX_TERMINATING
		  resource.setProperty(\"currentTask\", currentTask.toString())
		  plan = planner.goPlasticBoxMoves()
		"]
	} Goto exploringWithPlan
	
	State emptingDetector {
		println("STATE: emptingDetector")
		[" waitingForSupervisor = false
		   resource.setProperty(\"waitingForSupervisor\", waitingForSupervisor.toString())
		   val Quantity = NDB - SpaceAvailable "]
		request plasticbox -m throwaway : throwaway($Quantity)
	} Transition t4
		whenReply throwed -> throwedBottles
		
	State throwedBottles {
		println("STATE: throwedBottles")
		onMsg(throwed : throwed(QUANTITY)) {
			if "(payloadArg(0).toInt() == 0)" {
				[" waitingForSupervisor = true
				   resource.setProperty(\"waitingForSupervisor\", waitingForSupervisor.toString()) "]
			} else {
				[" SpaceAvailable += payloadArg(0).toInt()
		   		   resource.setProperty(\"SpaceAvailable\", SpaceAvailable.toString()) "]
			}
		}
	} Goto waitForSupervisorCommand if "(waitingForSupervisor)" else chooseDestination
	
	State chooseDestination {
		println("STATE: chooseDestination")
	} Goto goingHome if "(currentTask == DetectorTask.EMPTING_BOX_TERMINATING )" else goingLastPosition
	
	State goingLastPosition {
		println("STATE: goingLastPosition")
		["	currentTask = DetectorTask.GOING_BOTTLE
			resource.setProperty(\"currentTask\", currentTask.toString())
		  	plan = planner.getSafePlanMoves(lastPosition.first, lastPosition.second)	"]
	} Goto exploringWithPlan
	
	State waitForSupervisorCommand {
		println("STATE: waitForSupervisorCommand")
		println("waiting for a command from supervisor...")
	}
	Transition t5
 		whenMsg continue -> emptingDetector
	
	State destinationReached {
		println("STATE: destinationReached")
	} Goto discoveryHome if "(currentTask == DetectorTask.GOING_HOME )" else destinationReached2
	
	State destinationReached2 {
		println("STATE: destinationReached2")
	} Goto exploring if "(currentTask == DetectorTask.GOING_BOTTLE )" else emptingDetector
}