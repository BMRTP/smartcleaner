System smartcleaner

Dispatch explore   : explore(X)
Dispatch suspend   : suspend(X)
Dispatch terminate : terminate(X)
Dispatch continue  : continue(X)

Dispatch cmd      : cmd(X)

Request  step     : step(DURATION)
Request  backstep : backstep(DURATION)
Reply    stepdone : stepdone(X)
Reply    stepfail : stepfail(DURATION)

Request getobstacletype : getobstacletype(X)
Reply   obstacletype    : obstacletype(X)

Request  grab     : grab(X)
Reply    grabbed  : grabbed(X) //true, false

Request throwaway : throwaway(QUANTITY)
Reply   throwed : throwed(QUANTITY)

Dispatch init : init(NDB, WITHMAP)

Context ctxDetector ip [ host= "localhost" port= 8022 ]  
Context ctxRobot ip [ host= "127.0.0.1" port= 8018 ]  
Context ctxPlasticBox ip [ host= "127.0.0.1" port= 8016 ]

ExternalQActor smartrobot context ctxRobot
ExternalQActor obstacleclassifier context ctxRobot
ExternalQActor grabber context ctxRobot
ExternalQActor plasticbox context ctxPlasticBox

QActor detector context ctxDetector { 
	[" 
	   val resource = itunibo.resource.CoapResourceSupport(name, \"coap://localhost:5683\")
	   var NDB = 5
	   var SpaceAvailable = NDB
	   resource.createProperty(\"SpaceAvailable\", SpaceAvailable.toString())

	   var RoomMap = \"\" 
	   resource.createProperty(\"RoomMap\", RoomMap)

	   var currentTask = \"\" //exploring, goinghome, emptingBox, emptingBoxTerminating, goingBottle
	   resource.createProperty(\"currentTask\", currentTask)

	   var waitingForSupervisor: Boolean = false
	   resource.createProperty(\"waitingForSupervisor\", waitingForSupervisor.toString())

	   val mapname     = \"roomMbot3\"

	   //VIRTUAL ROBOT
       var StepTime = 330
	   var BackStepTime = 0
	   var completedPerimeter = false
	   var wasABottle = false

	   var currentPosition = Pair(1,1)
	   var lastPosition = currentPosition
	   var nextMoves = listOf<String>().iterator()
	   var nextMove = \"\"

	   var WaitCommandTime = 5L
		"]
	State start initial {  
		println("detector starts")
		run itunibo.planner.plannerUtil.initAI()
	} Transition t
		whenMsg init -> initializing
		whenMsg explore -> exploring
 		whenMsg suspend -> goingHome
 		whenMsg terminate -> terminating
 		
	State initializing {
		onMsg(init : init(NDB, WITHMAP)) {
		  ["NDB = payloadArg(0).toInt()
				SpaceAvailable = NDB
			if (payloadArg(1) == \"true\") {
				val (a, b) = itunibo.planner.plannerUtil.loadRoomMap(\"roomMbot3\")
			    if (a > 0 && b > 0) {
				    completedPerimeter = true
				    RoomMap = itunibo.planner.plannerUtil.getMap()
				    resource.setProperty(\"RoomMap\", RoomMap)
				}
			} "]
			
		}
	} Goto discoveryHome
	
	State discoveryHome {
		println("STATE: discoveryHome")
		println("waiting for a command...")	
	}
	Transition commandReceived
 			whenMsg explore -> exploring
 			whenMsg suspend -> goingHome
 			whenMsg terminate -> terminating
	
	State exploring { 
		println("STATE: exploring")
	  [ "currentTask = \"exploring\" 
		resource.setProperty(\"currentTask\", currentTask) "]
	} Goto exploringWithPlan if "(completedPerimeter)" else continuePerimeter
	
	State updatePosition {
		println("STATE: updatePosition")
		["currentPosition = itunibo.planner.plannerUtil.getRobotPosition()
		  if (currentPosition.first == 1 && currentPosition.second == 1 && currentTask == \"exploring\") {
				completedPerimeter = true
		  }
		"]  
	}
	Goto exploringWithPlan if "(completedPerimeter || currentTask != \"exploring\")" else continuePerimeter
	 
	State continuePerimeter {
		println("STATE: continuePerimeter")
		forward smartrobot -m cmd : cmd(d)
		run itunibo.planner.plannerUtil.rotateRight90()
		delay 1000
	} Goto doStep
	
	State doStep {
		run startTimer()
	} Transition a
		whenTimeVar WaitCommandTime  -> stepping
		whenMsg explore -> exploring
 		whenMsg suspend -> goingHome
 		whenMsg terminate -> terminating
	
	State stepping {
		println("STATE: doStep")
		request smartrobot -m step : step($StepTime)
	}
	Transition t0   whenReply stepdone -> stepDone   
					whenReply stepfail -> stepFailed
	
	State stepDone {  
		println("STATE: stepDone")
 		run itunibo.planner.plannerUtil.updateMapAfterAheadOk()
		[" RoomMap = itunibo.planner.plannerUtil.getMap()
		   resource.setProperty(\"RoomMap\", RoomMap) "]		
		delay 1000
 	}
	Goto updatePosition
	
	State stepFailed {  
		println("STATE: stepFailed")
 		onMsg(stepfail : stepfail(DURATION)) {
 			println("detector founds an obstacle after ${payloadArg(0)}")
 			["BackStepTime = payloadArg(0).toInt() - 20"] //TODO tune compensation
 			request obstacleclassifier -m getobstacletype : getobstacletype(type)
 		}
	}
	Transition t1
		whenReply obstacletype -> checkObstacleType
    
    State checkObstacleType {
    	println("STATE: checkObstacleType")
    	onMsg(obstacletype : obstacletype(X)) {
 			println("detector founds ${payloadArg(0)}")
 			["wasABottle = (payloadArg(0).contains(\"bottle\", ignoreCase = true))"]
 			if "(wasABottle)" {
 				if "(SpaceAvailable > 0)" {
 					request grabber -m grab : grab(it)
 				} else {
 					//already dirty cell
 					["lastPosition = itunibo.planner.plannerUtil.getAHeadPosition() "]
 					request smartrobot -m backstep : backstep($BackStepTime)
 				}
 			} else {
 				run itunibo.planner.plannerUtil.setObstacleOnCurrentDirection()
 				request smartrobot -m backstep : backstep($BackStepTime)
 			}
 			[" RoomMap = itunibo.planner.plannerUtil.getMap()
		   resource.setProperty(\"RoomMap\", RoomMap) "]		
 		}
    }
    Transition t2
    	whenReply grabbed -> bottleGrabbed
    	whenReply stepdone -> backCompensationDone
    	
    State backCompensationDone {
    	println("STATE: backCompensationDone")
    } Goto emptingBox if "(wasABottle)" else doNextStep
    
    State doNextStep {
    	println("STATE: doNextStep")
    	if "(!completedPerimeter)" {
	    	forward smartrobot -m cmd : cmd(a)
	    	run itunibo.planner.plannerUtil.rotateLeft90()
	    	delay 1000
    	}
    } 
    Goto doStep if "(!completedPerimeter)" else resetPlan
    	
    State bottleGrabbed {
    	println("STATE: bottleGrabbed")
    	onMsg(grabbed : grabbed(X)) {
    		if "(payloadArg(0) == \"true\")" {
 				println("detector grabbed a bottle")
 				[" SpaceAvailable -= 1
		   		   resource.setProperty(\"SpaceAvailable\", SpaceAvailable.toString()) "]
 			} else {
 				println("FATAL ERROR: detector can't grab the object")
 			}
 			request smartrobot -m backstep : backstep($BackStepTime)
    	}
    }
    Transition t3
    	 whenReply stepdone -> doStep
  	
  	State resetPlan {
  		println("STATE: resetPlan")
  		["nextMoves = listOf<String>().iterator()"]
  	} Goto exploringWithPlan
  	
  	State exploringWithPlan {
  		println("STATE: exploringWithPlan")
  		if "(!nextMoves.hasNext() && !itunibo.planner.plannerUtil.isRoomClean() && currentTask == \"exploring\")" {
  		  ["val (x,y) = itunibo.planner.plannerUtil.getNextDirtyCell()
		  println(\"goto [$x, $y]\")
		  nextMoves = itunibo.planner.plannerUtil.getPlanMoves(x, y)
		  println(itunibo.planner.plannerUtil.getPlanMoves(x, y).asSequence().toList())
		if(!nextMoves.hasNext()) {
			itunibo.planner.plannerUtil.setObstacle(x, y)
			RoomMap = itunibo.planner.plannerUtil.getMap()
		    resource.setProperty(\"RoomMap\", RoomMap)
		  } "]
  		} 
  	} Goto terminating if "(itunibo.planner.plannerUtil.isRoomClean() && currentTask == \"exploring\")" else takeNextMove
  	
  	State takeNextMove {
  		println("STATE: takeNextMove")
  		["nextMove = \"\" "]
  		if "(nextMoves.hasNext())" {
  			["nextMove = nextMoves.next()"]
  		}
  	} Goto planDone if "(nextMove == \"\")" else checkW
  	
  	State planDone {
  		println("STATE: planDone")
  	} Goto exploringWithPlan if "(currentTask == \"exploring\")" else destinationReached
  	
  	State checkW {
  		println("STATE: checkW")
  	} Goto doStep if "(nextMove == \"w\")" else checkD
  	
  	State checkD {
  		println("STATE: checkD")
  	} Goto rotateRight if "(nextMove == \"d\")" else checkA
  	
  	State rotateRight {
  		println("STATE: rotateRight")
  		forward smartrobot -m cmd : cmd(d)
		run itunibo.planner.plannerUtil.rotateRight90()
		delay 1000
  	} Goto exploringWithPlan
  	
  	State checkA {
  		println("STATE: checkA")
  	} Goto rotateLeft if "(nextMove == \"a\")" else exploringWithPlan
  	
  	State rotateLeft {
  		println("STATE: rotateLeft")
  		forward smartrobot -m cmd : cmd(a)
	    run itunibo.planner.plannerUtil.rotateLeft90()
	    delay 1000
  	} Goto exploringWithPlan
	
	State emptingBox {
		println("STATE: emptingBox")
		["currentTask = \"emptingBox\"
		  resource.setProperty(\"currentTask\", currentTask) 
 		  nextMoves = itunibo.planner.plannerUtil.goPlasticBoxMoves() "]
	} Goto exploringWithPlan
	
	State goingHome {
		println("STATE: goingHome")
		println("Detector going home")
		["currentTask = \"goinghome\"
		  resource.setProperty(\"currentTask\", currentTask)
		  nextMoves = itunibo.planner.plannerUtil.goHomeMoves()
		"]
	} Goto exploringWithPlan
	
	State terminating {
		println("STATE: terminating")
		println("Detector terminated the work")
	} Goto goingHome if "(SpaceAvailable == NDB)" else goingPlasticBox
	
	State goingPlasticBox {
		println("STATE: goingPlasticBox")
		println("Detector going to plasticbox")
		["currentTask = \"emptingBoxTerminate\"
		  resource.setProperty(\"currentTask\", currentTask)
		  nextMoves = itunibo.planner.plannerUtil.goPlasticBoxMoves()
		"]
	} Goto exploringWithPlan
	
	State emptingDetector {
		println("STATE: emptingDetector")
		[" waitingForSupervisor = false
		   resource.setProperty(\"waitingForSupervisor\", waitingForSupervisor.toString())
		   val Quantity = NDB - SpaceAvailable "]
		request plasticbox -m throwaway : throwaway($Quantity)
	} Transition t4
		whenReply throwed -> throwedBottles
		
	State throwedBottles {
		println("STATE: throwedBottles")
		onMsg(throwed : throwed(QUANTITY)) {
			if "(payloadArg(0).toInt() == 0)" {
				[" waitingForSupervisor = true
				   resource.setProperty(\"waitingForSupervisor\", waitingForSupervisor.toString()) "]
			} else {
				[" SpaceAvailable += payloadArg(0).toInt()
		   		   resource.setProperty(\"SpaceAvailable\", SpaceAvailable.toString()) "]
			}
		}
	} Goto waitForSupervisorCommand if "(waitingForSupervisor)" else chooseDestination
	
	State chooseDestination {
		println("STATE: chooseDestination")
	} Goto goingHome if "(currentTask == \"emptingBoxTerminate\" )" else goingLastPosition
	
	State goingLastPosition {
		println("STATE: goingLastPosition")
		["	currentTask = \"goingBottle\"
			resource.setProperty(\"currentTask\", currentTask)
		  	nextMoves = itunibo.planner.plannerUtil.getSafePlanMoves(lastPosition.first, lastPosition.second)	"]
	} Goto exploringWithPlan
	
	State waitForSupervisorCommand {
		println("STATE: waitForSupervisorCommand")
		println("waiting for a command from supervisor...")
	}
	Transition t5
 		whenMsg continue -> emptingDetector
	
	State destinationReached {
		println("STATE: destinationReached")
	} Goto discoveryHome if "(currentTask == \"goinghome\" )" else destinationReached2
	
	State destinationReached2 {
		println("STATE: destinationReached2")
	} Goto exploring if "(currentTask == \"goingBottle\" )" else emptingDetector
}